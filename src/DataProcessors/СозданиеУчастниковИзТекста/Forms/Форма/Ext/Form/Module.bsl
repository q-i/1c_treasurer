
&НаСервере
Процедура СоздатьУчастниковНаСервере()
	
	МассивИмен = Новый Массив;
	
	Для Сч = 1 По ПереченьУчастников.КоличествоСтрок() Цикл
		Стр = СокрЛП(ПереченьУчастников.ПолучитьСтроку(Сч));
		Если ПустаяСтрока(Стр) Тогда
			Продолжить;
		КонецЕсли;
		МассивИмен.Добавить(Стр);
	КонецЦикла;
	
	Если МассивИмен.Количество() = 0 Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не заполнены участники!");
		Возврат;
	КонецЕсли;
	
	СуществующиеУчастники = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Участники.Ссылка КАК Ссылка,
	|	Участники.Наименование КАК Наименование
	|ИЗ
	|	Справочник.Участники КАК Участники
	|ГДЕ
	|	Участники.Наименование В(&МассивИмен)";
	Запрос.УстановитьПараметр("МассивИмен", МассивИмен);
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			СуществующиеУчастники.Вставить(Выборка.Наименование, Выборка.Ссылка);
		КонецЦикла;
	КонецЕсли; 
	
	Для Каждого ТекИмя Из МассивИмен Цикл
		Если СуществующиеУчастники.Получить(ТекИмя) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		НовыйЭлемент = Справочники.Участники.СоздатьЭлемент();
		НовыйЭлемент.Наименование = ТекИмя;
		НовыйЭлемент.Записать();
		СуществующиеУчастники.Вставить(ТекИмя, НовыйЭлемент.Ссылка);
	КонецЦикла;
		
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура СоздатьУчастников(Команда)
	
	Ответ = Ждать ВопросАсинх("Создать участников?", РежимДиалогаВопрос.ДаНетОтмена);
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	СоздатьУчастниковНаСервере();
	
КонецПроцедуры
