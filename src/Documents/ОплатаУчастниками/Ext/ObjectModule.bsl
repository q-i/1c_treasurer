
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)

	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.УчастиеВМероприятии") Тогда
		// Заполнение шапки
		ОплатыВТабличнойЧасти = Истина;
		// Заполнение таб.части
		Для Каждого СтрокаТЧ Из ДанныеЗаполнения.Участники Цикл
			НоваяСтрока = Оплаты.Добавить();
			НоваяСтрока.Мероприятие = ДанныеЗаполнения.Мероприятие;
			НоваяСтрока.Участник = СтрокаТЧ.Участник;
			НоваяСтрока.Сумма = ДанныеЗаполнения.Мероприятие.СтоимостьУчастия;
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)

	Движения.ЗадолженностьУчастников.Записывать = Истина;
	Движения.ЗадолженностьКазначея.Записывать = Истина;
	
	Если ОплатыВТабличнойЧасти Тогда
		Для Каждого СтрокаТЧ Из Оплаты Цикл
			// регистр Задолженность Расход
			Движение = Движения.ЗадолженностьУчастников.Добавить();
			Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
			Движение.Период = Дата;
			Движение.Мероприятие = СтрокаТЧ.Мероприятие;
			Движение.Участник = СтрокаТЧ.Участник;
			Движение.Сумма = СтрокаТЧ.Сумма;
			// регистр ЗадолженностьКазначея Приход
			Движение = Движения.ЗадолженностьКазначея.Добавить();
			Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
			Движение.Период = Дата;
			Движение.Мероприятие = СтрокаТЧ.Мероприятие;
			Движение.Сумма = СтрокаТЧ.Сумма;
		КонецЦикла;
	Иначе 
		// регистр Задолженность Расход
		Движение = Движения.ЗадолженностьУчастников.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период = Дата;
		Движение.Мероприятие = Мероприятие;
		Движение.Участник = Участник;
		Движение.Сумма = Сумма;
		// регистр ЗадолженностьКазначея Приход
		Движение = Движения.ЗадолженностьКазначея.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Период = Дата;
		Движение.Мероприятие = Мероприятие;
		Движение.Сумма = Сумма;
	КонецЕсли;

КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОплатыВТабличнойЧасти Тогда
		МассивМероприятий = ОбщегоНазначения.ПолучитьМассивУникальныхЗначенийКолонки(Оплаты, "Мероприятие");
		Мероприятие = ?(МассивМероприятий.Количество() = 1, МассивМероприятий[0], Справочники.Мероприятия.ПустаяСсылка());
		МассивУчастников = ОбщегоНазначения.ПолучитьМассивУникальныхЗначенийКолонки(Оплаты, "Участник");
		Участник = ?(МассивУчастников.Количество() = 1, МассивУчастников[0], Справочники.Участники.ПустаяСсылка());
		Сумма = Оплаты.Итог("Сумма");
	Иначе
		Оплаты.Очистить();
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)

	Если ОплатыВТабличнойЧасти Тогда
		ПроверяемыеРеквизиты.Добавить("Оплаты.Мероприятие");
		ПроверяемыеРеквизиты.Добавить("Оплаты.Участник");
	Иначе
		ПроверяемыеРеквизиты.Добавить("Мероприятие");
		ПроверяемыеРеквизиты.Добавить("Участник");
	КонецЕсли;

КонецПроцедуры




